import run.BottleTwin;

conveyorPlant(
		input signal motConveyorOnOff, enable;
		
		// real world sim signal a bottle has been placed
		input BottleTwin signal bottleArriveToZero, bottleArriveToFive, roturn;
		
		output signal bottleAtPos1, bottleLeftPos5;
		
		// backend update
		output signal pos0, pos1, pos5, pos7;

)
->
{
	signal conveyorMove;
	BottleTwin signal posZero, posOne, posFive, posSeven;
	{
		while(true) {
			await(motConveyorOnOff);
			await(enable);
			abort(!motConveyorOnOff){
				BottleTwin bZero = (BottleTwin)#posZero;
				BottleTwin bFive = (BottleTwin)#posFive;

				if(bZero != null) {
					emit posOne(bZero);
					emit posZero;
					emit pos0(-1);

				}
				if(bFive != null) {
					emit posSeven(bFive);
					emit posFive(null);
					emit pos5(-1);
				}

			}
		}
	}
	
	||
	
	{
		while(true) {
			await(bottleArriveToZero);
			BottleTwin b = (BottleTwin)#bottleArriveToZero;
			if(b != null) {
				int id = b.ID;
				emit pos0(id);
				emit posZero(b);
			}

		}
		
	}
	
	||
	
	{
		while(true) {
			await(bottleArriveToFive);
			BottleTwin b = (BottleTwin)#bottleArriveToFive;
			if(b != null) {
				int id = b.ID;
				emit pos5(id);
				emit posFive(b);
			}
		}
	}
	
	||
	
	// real world sim
	{
		while(true) {
			await(roturn);
			emit posOne(null);
		}
	}
	
	||
	
	{
		while(true) {
			await(posOne);
			BottleTwin b = (BottleTwin)#posOne;
			if(b != null) {

				int id = b.ID;
				abort(roturn){
					emit pos1(id);
					sustain bottleAtPos1;
				}
				emit pos1(-1);
			}
		}
	}

}